buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'de.marcphilipp.nexus-publish' version '0.2.0'
}

apply plugin: 'java'
apply plugin: 'signing'
apply plugin: "maven-publish"

group = 'com.yaoruozhou'
version = '0.1.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.projectlombok:lombok:1.18.26'
    annotationProcessor 'org.projectlombok:lombok:1.18.26'
    implementation("io.cucumber:cucumber-java:6.11.0")
    implementation 'com.github.leeonky:jfactory-cucumber:0.1.24'
    implementation 'org.springframework.data:spring-data-mongodb:4.0.3'

    testImplementation("io.cucumber:cucumber-spring:6.11.0")
    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.0.3'
    testImplementation 'org.springframework.boot:spring-boot-starter-web:3.0.3'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.26'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            artifact sourcesJar
            artifact javadocJar
            pom {
                name = project.name
                description = project.name
                url = 'https://github.com/nerds-odd-e/' + project.name

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'josephyao'
                        name = 'Joseph Yao'
                        email = 'joseph@odd-e.com'
                    }
                }

                scm {
                    connection = 'scm:git:https://github.com/nerds-odd-e/' + project.name + '.git'
                    developerConnection = 'scm:git:git@github.com:nerds-odd-e' + project.name + '.git'
                    url = 'https://github.com/nerds-odd-e/' + project.name + '.git'
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

nexusPublishing {
    serverUrl = uri("https://s01.oss.sonatype.org/service/local/")
    username = System.env.ossrhUsername
    password = System.env.ossrhPassword
}

configurations {
    cucumberRuntime {
        extendsFrom testImplementation
    }
}

task cucumber(type: JavaExec) {
    dependsOn assemble, testClasses
    main = "io.cucumber.core.cli.Main"
    classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
    args = ['--plugin', 'pretty',
            '--glue', 'com.yaoruozhou.jfactory.cucumber',
            '--glue', 'com.github.leeonky.jfactory.cucumber',
            'src/test/resources']
}
